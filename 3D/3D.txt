 //Μεταβλητές κάμερας
  float x,y,z; // Η θέση της κάμερας
  float tx,ty,tz; // Η θέση όπου κοιτά η κάμερα
  float angle; //Γωνία κάμερας
 
  //Μεταβλητές κίνησης
  
  int moveX; // Η τιμή όπου θα προσθέτουμε στο Χ
  int moveZ; // Η τιμή όπου θα προσθέτουμε στο Ζ

  //Σταθερές
  
  int boxSize = 90; // Μέγεθος του κουτιού
  int increaseValue = 0; // Μέγεθος αύξησης κουτιού
  
  int standHeight = 100; //Το ύψος που θα είμαστε απ το έδαφος
  float sensitivity = 15; //Πόσο γρήγορα θα κουνιέται η κάμερα    
  int stillBox = 100;    //Το ποντίκι πρέπει να είναι stillBox μακρυά από το κουτί για να μετακινηθεί
  float cameraDistance = 1000; // Η απόσταση της κάμερας

void setup(){
  size(800,600,P3D); //Δημιουργία του τρισδιάστατου χώρου
  noStroke();
   
  //Μεταβλητές κάμερας
  x = width/2; //Η θέση Χ της κάμερας
  y = height/2; //Η θέση Υ της κάμερας
  y-= standHeight; //Η θέση Υ της κάμερας - standHeight
  z = (height/2.0) / tan(PI*60.0 / 360.0); //Η θέση Ζ της κάμερας
  tx = width/2; //Η θέση Χ για το που θα βλέπει η κάμερα
  ty = height/2; //Η θέση Υ για το που θα βλέπει η κάμερα
  tz = 0; //Η θέση Ζ για το που θα βλέπει η κάμερα

    angle = 0;
   
  //Μεταβλητές κίνησης
  moveX = 0;//Η τιμή moveX  η οποία προστίθεται στο Χ και tx της κάμερας 
  moveZ = 0;//Η τιμή moveZ  η οποία προστίθεται στο Z και tz της κάμερας 


}
 

void draw(){
   background(0);//Ζωγραφίζουμε μαύρο τον φόντο
   translate(width/2,height/2);//Θέτουμε την θέση ώστε ότι ζωγραφίσουμε να πάνε εκεί
   //pointLight(255,255,255,x,y,z);//Εναλακτικός Φωτισμός
   lights();//Φωτισμός
   fill(255,255,0);//Ζωγραφίζουμε τον κύβο
   boxSize += increaseValue;//Προσθέτουμε την τιμή increaseValue στο μέγεθος του κύβου
   box(boxSize); //Φτιάχνουμε τον κύβο
   cameraUpdate();//Ενημερώνουμε την κάμερα
   locationUpdate();//Ενημερώνουμε την θέση μας
   camera(x,y,z,tx,ty,tz,0,1,0); // Δημιουργούμε την κάμερα
}
 
public void cameraUpdate(){
 
    int diffX = mouseX - width/2;//Η διαφορά της θέσης Χ του ποντικιού απ το πλάτος/2
    int diffY = mouseY - width/2;//Η διαφορά της θέσης Υ του ποντικιού απ το πλάτος/2
     //Έλεγχος αν το ποντίκι έφυγε απ την εστίαση του κουτιού
    if(abs(diffX) > stillBox){
     angle+= diffX/(sensitivity*10);//Παίρνουμε την γωνία

      if(angle < 0) //Όταν γίνετε παραπάνω από 360 μοίρες περιστροφή να μην προσθέτοι παραπάνω από 360
        angle += 360;
      else if (angle >= 360)
        angle -= 360;
      
     float newXComp = cameraDistance * sin(radians(angle));
     float newZComp = cameraDistance * cos(radians(angle));
//Παίρνουμε το ημίτονο και το συνημίτονο από την κάμερα τον κύβο και την θέση του ποντικιού και τα προσθέτουμε στο tx και τζ της κάμερας. 
     tx = newXComp + x;
     tz = -newZComp + z;

    }
    
    if (abs(diffY) > stillBox)//Προσθέτουμε στο ty την διαφορά του mouseΥ - width/2
      ty += diffY/(sensitivity/1.5);
}
 



public void locationUpdate(){
  //Προσθέτουμε τις αντίστοιχες μεταβλητές ώστε να μετακινείτε η θέση της κάμερας  
    z += moveZ;
    tz += moveZ;
    x += moveX;
    tx += moveX;
}

public void keyPressed(){
  if (keyCode=='=')//Ορίζουμε την μεταβλητή increaseValue ώστε να προσθεθέι στην draw στην μεταβλητή boxSize.
    increaseValue = 10;
  if (keyCode=='-')
    increaseValue = -10;
  if(keyCode == UP || key == 'w'){
    moveZ = -10;}
  else if(keyCode == DOWN || key == 's'){
    moveZ = 10;}
  else if(keyCode == LEFT || key == 'a'){
    moveX = -10;
  }
  else if(keyCode == RIGHT || key == 'd'){
    moveX = 10;
  }
//Ορίζουμε τιμές στα moveZ και moveX ώστε να προσθέσει στην αντίστοιχη τιμή στην μέθοδο locationUpdate()
}
 
public void keyReleased(){
  if (keyCode=='=')//Ορίζουμε την μεταβλητή increaseValue ώστε να προσθεθέι στην draw στην μεταβλητή boxSize.
    increaseValue = 0;
  if (keyCode=='-')
    increaseValue = 0;
  if(keyCode == UP || key == 'w'){
    moveZ = 0;
  }
  else if(keyCode == DOWN || key == 's'){
    moveZ = 0;
  }
  else if(keyCode == LEFT || key == 'a'){
    moveX = 0;
  }
  else if(keyCode == RIGHT || key == 'd'){
    moveX = 0;//Ορίζουμε τιμές στα moveZ και moveX ώστε να προσθέσει στην αντίστοιχη τιμή στην μέθοδο locationUpdate()
  }
}